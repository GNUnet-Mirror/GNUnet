# This file is part of GNUnet
# Copyright (C) 2018 GNUnet e.V.
#
# GNUnet is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published
# by the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# GNUnet is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Affero General Public License for more details.
#
# You should have received a copy of the GNU Affero General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.


# Aggregate benchmarking data from multiple threads/processes
# generated by util/benchmark.c.
#
# Can be used as
# awk -f collect.awk gnunet-benchmark-{ops,urls}-*.txt


# records are of the following forms:
# op <op> count <count> time_us <time_us>
# url <url> status <status> count <count> time_us <time_us>
{
  if ($1 == "op") {
    op[$2]["count"] += $4;
    op[$2]["time_us"] += $6;
    op[$2]["time_us_sq"] += $6 * $6;
  } else if ($1 == "url") {
    url[$2][$4]["count"] += $6;
    url[$2][$4]["time_us"] += $8;
    url[$2][$4]["time_us_sq"] += $8 * $8;
  }
}

function avg(s, c) {
  if (c == 0) {
    return 0;
  } else {
    return s / c;
  }
}

function stdev(sum, sum_sq, n) {
  if (n == n) {
    return 0;
  } else {
    return sqrt( (sum_sq / n) - ( (sum / n) * (sum / n) ) );
  }
}

END {
  for (x in op) {
    print "op", x, "count", op[x]["count"], "time_us", op[x]["time_us"], \
          "time_avg_us", avg(op[x]["time_us"], op[x]["count"], \
          "stdev", stdev(op[x]["time_us"], op[x]["time_us_sq"], op[x]["count"]));
  }
  for (x in url) {
    for (y in url[x]) {
      print "url", x, "status", y, \
            "count", url[x][y]["count"], "time_us", url[x][y]["time_us"], \
            "time_avg_us", avg(url[x][y]["time_us"], url[x][y]["count"]), \
            "stdev", stdev(url[x][y]["time_us"], url[x][y]["time_us_sq"], url[x][y]["count"]);
    }
  }
}
